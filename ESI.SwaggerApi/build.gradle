// - P L U G I N S
plugins {
    id 'de.undercouch.download' version '3.4.3'
    id "org.hidetake.swagger.generator" version "2.18.1"
}
apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'de.undercouch.download'
apply plugin: 'org.hidetake.swagger.generator'

// - P R O J E C T   D E F I N I T I O N
group = 'org.dimensinfin.neocom'
version = '0.20.6'
archivesBaseName = 'esi.swagger-api'
description = 'Swagger Api to access ESI published Eve Online inside game data structures.'

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

repositories {
    mavenLocal()
    mavenCentral()
    google()
    jcenter()
    maven { url 'https://jitpack.io' }
}

// - S O U R C E S
def swaggerGenerated = "$projectDir/generated/"
sourceSets {
    main {
        java {
            srcDirs += "$swaggerGenerated/src/main/java"
        }
    }
    test {
        java {
            srcDirs += "$swaggerGenerated/src/test/java"
        }
    }
}

// - S W AG G E R   T A S K S
task swaggerDownload(type: Download) {
    src 'https://esi.evetech.net/latest/swagger.json'
    dest projectDir
    acceptAnyCertificate true
    onlyIfNewer true
    compress false
}

task swaggerGenerate(type: GenerateSwaggerCode) {
    language = 'java'

    inputFile = file(new File(projectDir, 'swagger.json'))
    outputDir = file(swaggerGenerated)
    configFile = file('swagger-config.json')
}

task swaggerBuild(type: Delete, dependsOn: swaggerGenerate) {
    delete "$swaggerGenerated/docs"
    delete "$swaggerGenerated/gradle"
    delete "$swaggerGenerated/src/test/"
    delete "$swaggerGenerated/src/main/AndroidManifest.xml"
    // Remove the client code because it is not used.
    delete "$swaggerGenerated/src/main/java/org/dimansinfin/eveonline/neocom/esiswagger/client"
    delete fileTree("$swaggerGenerated").include('*')
}

task swaggerDelete(type: Delete) {
    delete "$swaggerGenerated"
}

clean.doFirst {
    delete "$swaggerGenerated"
}

// - T A S K   C O O R D I N A T I O N
compileJava.dependsOn swaggerBuild

// - P U B L I S H I N G
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}
publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            version = project.version
            artifactId = project.archivesBaseName
            artifact(sourcesJar)
            artifact(javadocJar)

            from components.java
            pom {
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'AdamAntinoo'
                        name = 'Adam Antinoo'
                        email = 'adamantinoo.git@gmail.com'
                    }
                }
            }
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
    repositories {
        mavenLocal()
        maven {
            url "$buildDir/repo"
        }
        maven {
            url "production"
        }
    }
}
publish.dependsOn build

// - P R O D U C T   V E R S I O N S
ext {
    jacksonVersion = '2.8.6'
    ormliteVersion = '4.48'
}

// - D E P E N D E N C I E S
dependencies {
    // - C O R E
    compileOnly 'joda-time:joda-time:2.9.9'
    compileOnly 'javax.annotation:javax.annotation-api:1.2'

    // - E S I . S W A G G E R A P I
    swaggerCodegen 'io.swagger:swagger-codegen-cli:2.2.1'
    compileOnly 'io.swagger:swagger-annotations:1.5.17'
    compileOnly 'com.squareup.retrofit2:retrofit:2.3.0'
    compileOnly 'com.squareup.retrofit2:converter-gson:2.3.0'
    compileOnly 'com.squareup.retrofit2:converter-scalars:2.3.0'
    compileOnly 'org.apache.oltu.oauth2:org.apache.oltu.oauth2.client:1.0.1'
    compileOnly 'org.apache.httpcomponents:httpclient:4.5.4'
}
